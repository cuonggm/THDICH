# Bai 3
.data
	A: .word 7, -2, 5, 1, 5, 6, 7, 3
.text
	la $a0, A			# a0 = &A[0]
	addi $a1, $0, 8		# tong so phan tu cua mang
	addi $a1, $a1, -1		# a1 = index cuoi cung
	jal BubbleSort
	j endmain
	
BubbleSort: # sap xep noi bot
	# luu tru t1, t2 (i va j), t0 (ket qua so sanh), t3, t4 (dia chi j, j+1), t5, t6 (noi dung cua phan tu j, j+1)
	addi $sp, $sp, -36
	sw $t0, 24($sp)
	sw $t1, 20($sp)
	sw $t2, 16($sp)
	sw $t3, 12($sp)
	sw $t4, 8($sp)
	sw $t5, 4($sp)
	sw $t6, 0($sp)
	sw $t7, 28($sp)
	sw $ra 32($sp)
	# thuc hien chuong trinh con
	add $t1, $0, $a1	# for (i = n-1
	forI:
	sgt $t0, $t1, 0		# ; i > 0 ;
	beq $t0, $0, endForI		# dieu kien i khong thoa man thi ket thuc for I
		addi $t2, $0, 0	# for (j = 0
		forJ:
		slt $t0, $t2, $t1	# ; j<i ;
		beq $t0, $0, endForJ	# dieu kien j khong thoa man
			# t5 = a[j]
			mul $t7, $t2, 4
			add $t3, $t7, $a0
			lw $t5, 0($t3)
			# t6 = a[j+1]
			addi $t7, $t2, 1
			mul $t7, $t7, 4
			add $t4, $t7, $a0
			lw $t6, 0($t4)
			
			# kiem tra neu a[j] > a[j+1] thi swap
			sgt $t0, $t5, $t6
			beq $t0, $0, endif
				# gan tham so swap a2 = t3, a3 = t4
				add $a2, $t3, $0
				add $a3, $t4, $0
				jal Swap
			endif:
			
			addi $t2, $t2, 1	# j = j +1
			j forJ
		endForJ:
		addi $t1, $t1, -1
		j forI
	endForI:
	# tra lai gia tri cac thanh ghi
	lw $t0, 24($sp)
	lw $t1, 20($sp)
	lw $t2, 16($sp)
	lw $t3, 12($sp)
	lw $t4, 8($sp)
	lw $t5, 4($sp)
	lw $t6, 0($sp)
	lw $t7, 28($sp)
	lw $ra 32($sp)
	addi $sp, $sp, 36
	jr $ra
	
Swap:	# swap bien co dia chi luu o $a2 va $a3
	# luu tru $t1, $t2
	addi $sp, $sp, -8
	sw $t1, 4($sp)
	sw $t2, 0($sp)
	
	# thuc thi chuong trinh
	lw $t1, 0($a2)
	lw $t2, 0($a3)
	sw $t2, 0($a2)
	sw $t1, 0($a3)
	
	# tra lai gia tri ban dau cho $t1, $t2
	lw $t2, 0($sp)
	lw $t1, 4($sp)
	addi $sp, $sp, 8
	jr $ra
	

endmain:
